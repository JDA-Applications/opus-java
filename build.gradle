group = 'club.minnced'
project.version = '1.1.0'

allprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'club.minnced'
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    compile project(':api')
    compile project(':natives')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
}

build.dependsOn sourcesJar

publishing {
    publications {
        Release(MavenPublication) {
            groupId project.group
            artifactId project.name
            version version
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each {
                    def node = dependenciesNode.appendNode('dependency')
                    node.appendNode('groupId', it.group)
                    node.appendNode('artifactId', it instanceof ProjectDependency ? it.dependencyProject.moduleName : it.name)
                    node.appendNode('version' , it.version)
                    node.appendNode('scope', 'compile')
                }
            }

            repositories {
                maven {
                    url "s3://m2.dv8tion.net/releases"
                    credentials(AwsCredentials) {
                        accessKey findProperty("awsAccessKey")
                        secretKey findProperty("awsSecretKey")
                    }
                }
            }
        }
    }
}
