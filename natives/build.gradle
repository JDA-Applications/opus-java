plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray'
}

project.version = "1.0.5"
ext.moduleName = 'opus-java-natives'
archivesBaseName = moduleName

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
}

build.dependsOn sourcesJar

bintrayUpload {
    dependsOn build

    onlyIf { !getProjectProperty("bintrayUsername").empty }
    onlyIf { !getProjectProperty("bintrayApiKey").empty }
}

bintray {
    override = false
    dryRun = false
    user = bintrayUsername
    key = bintrayApiKey
    publications = ["BintrayRelease"]
    pkg {
        repo = 'maven'
        name = moduleName
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/discord-java/opus-java.git'
        githubRepo = 'discord-java/opus-java'
        publish = true
        version {
            name = project.version
            vcsTag = project.version
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}

String getProjectProperty(String propertyName) {
    return hasProperty(propertyName) ? this.properties[propertyName] : ""
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId project.group
            artifactId moduleName
            version version

            artifact sourcesJar
        }
    }
}